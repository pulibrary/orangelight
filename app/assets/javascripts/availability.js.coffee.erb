jQuery ->
  window.availability_updater = new AvailabilityUpdater
class AvailabilityUpdater
  constructor: ->
    this.request_availability()
    this.scsb_search_availability()
  availability_url: "<%= ENV['bibdata_base'] %>/availability"
  id = ''
  on_site_status = 'On-site'
  on_site_unavailable = 'On-site - '
  circ_desk = 'See front desk'
  available_statuses = ['Not charged', 'On shelf']
  returned_statuses = ['Discharged']
  in_process_statuses = ['In process', 'On-site - in process']
  checked_out_statuses = ['Charged', 'Renewed', 'Overdue', 'On hold',
    'In transit', 'In transit on hold', 'In transit discharged', 'At bindery',
    'Remote storage request', 'Hold request', 'Recall request']
  missing_statuses = ['Missing', 'Claims returned', 'Withdrawn']
  long_overdue_statuses = ['Lost--system applied']
  lost_statuses = ['Lost--library applied']
  available_labels = ['Available', 'Returned', 'In process', 'Requestable',
    'On shelf', 'All items available', 'On-site access']
  available_non_requestable_labels = ['Available', 'Returned', 'Requestable',
    'On shelf', 'All items available', 'On-site access', 'On-site - in-transit discharged']
  open_location_labels = ['Available', 'All items available']
  unavailable_labels = ['Checked out', 'Missing', 'Lost']
  request_availability: ->
    if $(".documents-list").length > 0
      ids = this.record_ids().toArray()
      return if ids.length < 1
      params = $.param({ids: ids})
      url = "#{@availability_url}?#{params}"
      $.getJSON(url, this.process_records)
        .fail (jqXHR, textStatus, errorThrown) =>
          console.error("Failed to retrieve availability data for the bib. records " + ids.join(", ") + ": " + errorThrown)

    else if $("*[data-availability-record='true']").length > 0
      id = window.location.pathname.split('/')[2]
      if id.match /^SCSB-\d+/
        url = "#{@availability_url}?scsb_id=#{id.replace /^SCSB-/, ''}"
        $.getJSON(url, this.process_scsb_single)
          .fail (jqXHR, textStatus, errorThrown) =>
            console.error("Failed to retrieve availability data for the SCSB record " + id + ": " + errorThrown)

      else
        url = "#{@availability_url}?id=#{id}"
        $.getJSON(url, this.process_single)
          .fail (jqXHR, textStatus, errorThrown) =>
            console.error("Failed to retrieve availability data for the bib. record " + id + ": " + errorThrown)

  scsb_search_availability: ->
    if $(".documents-list").length > 0
      barcodes = this.scsb_barcodes().toArray()
      return if barcodes.length < 1
      params = $.param({barcodes: barcodes})
      url = "#{@availability_url}?#{params}"
      $.getJSON(url, this.process_barcodes)
        .fail (jqXHR, textStatus, errorThrown) =>
          console.error("Failed to retrieve availability data for the SCSB barcodes " + barcodes.join(", ") + ": " + errorThrown)

  process_records: (records) =>
    for record_id, holding_records of records
      this.apply_record(record_id, holding_records)
  process_barcodes: (barcodes) =>
    for barcode_id, item_data of barcodes
      this.apply_scsb_record(barcode_id, item_data)
  process_single: (holding_records) =>
    for holding_id, availability_info of holding_records
      availability_element = $("*[data-availability-record='true'][data-record-id='#{id}'][data-holding-id='#{holding_id}'] .availability-icon")
      aeon = $("*[data-availability-record='true'][data-record-id='#{id}'][data-holding-id='#{holding_id}']").attr('data-aeon')
      if availability_info['label']
        location = $("*[data-location='true'][data-holding-id='#{holding_id}']")
        location.text(availability_info['label'])
      this.get_issues(holding_id) if $(".journal-current-issues").length > 0
      if availability_info['more_items']
        if title_case(availability_info['status']).match(on_site_status)
          this.apply_record_icon(availability_element, "On-site access", aeon, availability_info)
        else
          this.apply_record_icon(availability_element, "All items available", aeon, availability_info)
        this.get_more_items(holding_id, availability_info['label'])
      else
        this.apply_record_icon(availability_element, availability_info['status'], aeon, availability_info)
      if availability_info['temp_loc']
        current_map_link = $("*[data-holding-id='#{holding_id}'] .find-it")
        temp_map_link = stackmap_link(id, availability_info)
        current_map_link.replaceWith(temp_map_link)
      this.update_location_services(holding_id, availability_info)
  process_scsb_single: (item_records) =>
    if Object.keys(item_records).length > 1
      multi_items = true
      for barcode, availability_info of item_records
        if availability_info['itemAvailabilityStatus'] != 'Available'
          status_message = 'Some Items Not Available'
    for barcode, availability_info of item_records
      availability_element = $("*[data-availability-record='true'][data-record-id='#{id}'][data-scsb-barcode='#{barcode}'] .availability-icon")
      aeon = $("*[data-availability-record='true'][data-record-id='#{id}'][data-scsb-barcode='#{barcode}']").attr('data-aeon')
      availability_element.addClass("label")
      if aeon == 'true'
        availability_element.addClass("label-success")
        availability_element.text("On-Site Access")
        availability_element.attr("title", "Availability: On-site access by request")
      else if multi_items
        if status_message
          availability_element.addClass("label-default")
          availability_element.text(status_message)
          availability_element.attr("title", "Availability: Some items not available")
        else
          availability_element.addClass("label-success")
          availability_element.text('All Items Available')
          availability_element.attr("title", "Availability: All items available")
      else
        if availability_info['itemAvailabilityStatus'] == 'Available'
          availability_element.addClass("label-success")
          availability_element.text(availability_info['itemAvailabilityStatus'])
          availability_element.attr("title", "Availability: On shelf")
        else
          availability_element.addClass("label-danger")
          availability_element.text(availability_info['itemAvailabilityStatus'])
          availability_element.attr("title", "Availability: Checked out")
  update_location_services: (holding_id, availability_info) ->
    status = availability_info['status']
    temp_status = availability_info['temp_loc']
    location_services_element = $(".location-services[data-holding-id='#{holding_id}'] a")
    availability_label = $(".holding-status[data-holding-id='#{holding_id}'] .availability-icon.label")
    if availability_label.text()
      availability_label_text = title_case(availability_label.text())
    display_request = location_services_element.attr('data-requestable')
    if availability_label_text not in available_non_requestable_labels
      display_request = 'true'
    if title_case(status) == 'On-site - in transit discharged'
      display_request = 'false'
    if display_request == 'true'
      if temp_status
        location_services_element.hide()
      else
        location_services_element.show()
  apply_record: (record_id, holding_records) ->
    for holding_id, availability_info of holding_records
      if availability_info['label']
        location = $("*[data-location='true'][data-record-id='#{record_id}'][data-holding-id='#{holding_id}'] .results_location")
        aeon = $("*[data-record-id='#{record_id}'][data-holding-id='#{holding_id}']").attr('data-aeon')
        location.text(availability_info['label'])
      this.record_needs_more_info(record_id) if availability_info['more_items']
      availability_element = $("*[data-availability-record='true'][data-record-id='#{record_id}'][data-holding-id='#{holding_id}'] .availability-icon")
      if availability_info['temp_loc']
        current_map_link = $("*[data-location='true'][data-record-id='#{record_id}'][data-holding-id='#{holding_id}'] .find-it")
        $(availability_element).next('.icon-warning').hide()
        temp_map_link = stackmap_link(record_id, availability_info, true)
        current_map_link.replaceWith(temp_map_link)
      this.apply_record_icon(availability_element, availability_info['status'], aeon, {})
    true
  apply_scsb_record: (barcode, item_data) ->
    availability_element = $("*[data-scsb-availability='true'][data-scsb-barcode='#{barcode}']")
    if item_data['itemAvailabilityStatus'] == 'Available'
      availability_element.addClass("label-success")
      availability_element.text(item_data['itemAvailabilityStatus'])
      availability_element.attr("title", "Availability: On Shelf")
    else
      availability_element.addClass("label-danger")
      availability_element.text('Checked Out')
      availability_element.attr("title", "Availability: Checked Out")
    true
  get_more_items: (holding_id, holding_label) ->
    url = "#{@availability_url}?mfhd=#{holding_id}"
    req = $.getJSON url
            .fail (jqXHR, textStatus, errorThrown) =>
              console.error("Failed to retrieve availability data for the holding ID " + holding_id + ": " + errorThrown)

    element = $(".holding-details[data-holding-id='#{holding_id}']")
    req.success (data) ->
      ul = ""
      for key, item of data
        status = title_case(item['status'])
        label = status_label(status)
        if holding_label != item['label'] || item['temp_loc']
          li = "<li>#{item['enum_display'] || 'Item'}: #{item['label']} - #{status_display(status, label)}</li>"
          ul = ul + li
        if (status not in available_statuses && status != on_site_status)
          unless holding_label != item['label'] || item['temp_loc']
            li = "<li>#{item['enum_display'] || 'Item'}: #{status_display(status, label, item['due_date'])}</li>"
            ul = ul + li
          span = $("*[data-holding-id='#{holding_id}'] .availability-icon")
          txt = if status.match(on_site_unavailable)
            circ_desk
          else if label not in unavailable_labels && span.text() != "Some items not available"
            "Some items may not be available"
          else
            "Some items not available"
          span.text(txt)
          span.attr("title", "Availability: " + txt)
          span.attr("data-original-title", "Availability: " + txt)
          if !status.match(on_site_unavailable)
            span.removeClass("label-success")
            span.addClass("label-default")
            location_services_element = $(".location-services[data-holding-id='#{holding_id}']")
            location_services_element.show()
      element.append(ul) if ul != ""
  get_issues: (holding_id) ->
    url = "#{@availability_url}?mfhd_serial=#{holding_id}"
    req = $.getJSON url
            .fail (jqXHR, textStatus, errorThrown) =>
              console.error("Failed to retrieve availability data for the serial holding ID/MFHD " + holding_id + ": " + errorThrown)

    element = $("*[data-journal='true'][data-holding-id='#{holding_id}']")
    req.success (data) ->
      if data.length > 1
        element.before("<div class=\"holding-label current-issues\">Current issues:</div>")
        element.after("<div class=\"trigger\">More</div>")
      else if data != ''
        element.before("<div class=\"holding-label current-issues\">Current issues:</div>")
      for key, issue of data
        li = $("<li>#{issue}</li>")
        element.append(li)
  record_needs_more_info: (record_id) ->
    element = $("*[data-record-id='#{record_id}'] .more-info")
    element.addClass("label label-default")
    element.text("View record for full availability")
    element.attr('title', "Click on the record for full availability info")
    empty = $("*[data-record-id='#{record_id}'].empty")
    empty.removeClass("empty")
  apply_record_icon: (availability_element, status, aeon, availability_info) ->
    status = title_case(status)
    availability_element.addClass("label")
    label = status_label(status)
    availability_element.text("#{label}#{due_date(availability_info["due_date"])}")
    if label in unavailable_labels
      availability_element.addClass("label-danger")
    else if label in available_labels
      availability_element.addClass("label-success")
    else if label == 'On-site access'
      availability_element.addClass("label-warning")
    else if label == circ_desk
      availability_element.addClass("label-warning")
    else if label == 'Online'
      availability_element.addClass("label-primary")
    else
      availability_element.addClass("label-default")
    if aeon == 'true'
      status = "On-site access by request"
    availability_element.attr('title', "Availability: #{title_case(status)}")
    availability_element.attr('data-original-title', "Availability: #{title_case(status)}")
    availability_element.attr('data-toggle', 'tooltip')
  record_ids: ->
    $("*[data-availability-record='true'][data-record-id]").map((_, x) -> $(x).attr("data-record-id"))
  scsb_barcodes: ->
    $("*[data-scsb-availability='true'][data-scsb-barcode]").map((_, x) -> $(x).attr("data-scsb-barcode"))
  status_label = (status) ->
    switch
      when status in long_overdue_statuses then 'Long overdue'
      when status in lost_statuses then 'Lost'
      when status in available_statuses then 'Available'
      when status in returned_statuses then 'Returned'
      when status == 'In transit discharged' then 'In transit'
      when status in in_process_statuses then 'In process'
      when status in checked_out_statuses then 'Checked out'
      when status in missing_statuses then 'Missing'
      when status.match(on_site_unavailable) then circ_desk
      when status.match(on_site_status) then 'On-site access'
      when status.match('Order received') then 'Order received'
      when status.match('Pending order') then 'Pending order'
      when status.match('On-order') then 'On-order'
      else status
  status_display = (status, label, date_due) ->
    if status.match(label) || status.match(on_site_status)
      status
    else
      "#{label} #{due_date(date_due)} (#{status})"
  due_date = (date_string) ->
    return "" unless date_string?
    " - #{date_string}"
  title_case = (str) ->
    str[0].toUpperCase() + str[1..str.length - 1].toLowerCase()
  stackmap_link = (record_id, availability_info, marker_only) ->
    temp_status = availability_info['temp_loc']
    if temp_status
      location = availability_info['temp_loc']
    else
      location = availability_info['location']
    map_url = "/catalog/#{record_id}/stackmap?loc=#{location}"
    link = "<a title='Where to find it' class='find-it' data-location-map='#{location}' data-ajax-modal='trigger' href='#{map_url}'>"
    marker_span = "<span class='glyphicon glyphicon-map-marker'></span>"
    if marker_only
      link = "#{link}#{marker_span}</a>"
    else
      link = "#{link}<span class='link-text'>Where to find it</span>#{marker_span}</a>"
    link
